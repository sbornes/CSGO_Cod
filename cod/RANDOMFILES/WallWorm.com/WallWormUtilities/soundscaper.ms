/*
Soundscaper

Generate Skeleton Soundscape File 
from Soundscape entities in scene
for Source Engine

Copyright (c) 2011-2013
By Shawn Olson http://www.shawnolson.net

A Wall Worm Mod Tool
http://www.wallworm.com

This tool will find all soundscape entities in the scene and create a skeleton Soundscape script

Soundscapes can be added with either the WW Point Entity tools or with Convexity.

*/

soundscapeentities = for o in objects WHERE (isProperty o #entname AND o.entname == "env_soundscape" AND isProperty o #wallworm) OR (getUserProp o #GLBPntData != undefined AND (findString (getUserProp o #GLBPntData) "env_soundscape" == 1)) collect o

if soundscapeentities.count > 0 then (
	filename = getSaveFileName caption:"Save Soundscape File" filename:"soundscapes_mymapname.txt" types:"Sound Script File(*.txt)|*.txt"

	if filename != undefined then (
		local soundFile = openFile filename mode:"w+"

	)
	if filename == undefined OR soundFile == undefined then (
		messageBox "The Soundscape File was not saved."

	) else (
		str = "//Soundscape Script Partially generated by Wall Worm Soundscaper\n//http://www.wallworm.com\n\n//http://developer.valvesoftware.com/wiki/Soundscape\n\n\n//Sample SoundScript code\n\n\n "
		format str to: soundFile

		str = "//	\"playlooping\"\n"
		format str to: soundFile
		str = "//	{\n"
		format str to: soundFile
		str = "//		\"volume\"	\"0.3\"\n"
		format str to: soundFile
		str = "//		\"wave\"		\"path/to/sound.wav\"\n"
		format str to: soundFile
		str = "//		\"position\"	\"0\"\n"
		format str to: soundFile
		str = "//	}\n\n"
		format str to: soundFile
		str = "//	\"playsoundscape\"\n"
		format str to: soundFile
		str = "//	{\n"
		format str to: soundFile
		str = "//		name \"soundscapedefinedinthisfile\"\n"
		format str to: soundFile
		str = "//		\"position\" \"2\"\n"
		format str to: soundFile
		str = "//	}\n\n\n"
		format str to: soundFile

		for s in soundscapeentities do (
			if isProperty s #soundscape then (
				str = "\n\"" + (s.soundscape as String) + "\"\n{\n\n}\n\n"
				print str
				format str to: soundFile

			) else (
				GLBPntData = filterString (getUserProp s #GLBPntData) "," splitEmptyTokens:true
				scIndex = findItem GLBPntData "soundscape"

				if scIndex != 0 AND (getUserProp s #GLBPntValu != undefined) then (
					GLBPntValu = filterString (getUserProp s #GLBPntValu) "," splitEmptyTokens:true

					if GLBPntValu[scIndex] != undefined then (
						str = "\n\"" + (GLBPntValu[scIndex] as String) + "\"\n{\n\n}\n\n"
						print str
						format str to: soundFile

					) else (
						--print ("Not found at index "+ (scIndex as string))
					)
				)

			)

		)

	)
	if soundFile != undefined then (
		close soundFile
	)
) else (
	messageBox "There are no soundscape entities in the scene. Use Convexity to add Soundscape Point Entities."
)
